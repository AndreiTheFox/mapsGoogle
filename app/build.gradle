apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.dagger.hilt.android'

android {
    namespace "ru.netology.nmedia"

    compileSdkVersion 34

    defaultConfig {
        applicationId "ru.netology.nmedia"
        minSdkVersion 22
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        Properties properties = new Properties()
        if (rootProject.file("maps.properties").exists()) {
            properties.load(rootProject.file("maps.properties").newDataInputStream())
        }

        manifestPlaceholders.mapsApiKey = properties.getProperty("MAPS_API_KEY", "")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders.usesCleartextTraffic = false
            buildConfigField "String", "BASE_URL", '"https://netomedia.ru"'
        }
        debug {
            manifestPlaceholders.usesCleartextTraffic = true
            buildConfigField "String", "BASE_URL", '"http://10.0.2.2:9999"'
        }
    }
}

dependencies {
    def junit_version = "4.13.2"
    def ext_junit_version = "1.1.5"
    def espresso_core_version = "3.5.1"
    def maps_version = '18.2.0'
    def location_version = "21.0.1"
    def maps_ktx_version = '5.0.0'
    def hilt_version = "2.48.1"
    def android_navigation = "2.7.5"
    def room_version = "2.6.0"

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.10.0'
    //Coroutines for viewModels
    implementation 'androidx.activity:activity-ktx:1.8.0'
    //Fragments
    implementation "androidx.fragment:fragment-ktx:1.6.2"
    implementation "androidx.navigation:navigation-fragment-ktx:$android_navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$android_navigation"
    //Google maps
    implementation "com.google.android.gms:play-services-location:$location_version"
    implementation "com.google.android.gms:play-services-maps:$maps_version"
    implementation "com.google.maps.android:maps-ktx:$maps_ktx_version"
    implementation "com.google.maps.android:maps-utils-ktx:$maps_ktx_version"
    //Testing
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$ext_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"

    //Room Library
    implementation "androidx.room:room-common:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    //noinspection KaptUsageInsteadOfKsp
    kapt "androidx.room:room-compiler:2.6.0"
    //Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
}
